import java.time.LocalDateTime
plugins {
    id 'java'
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.21'
    id 'idea'
}

def name = 'Cosmos'
group 'fun.familyfunforce.cosmos'
version '1.0-SNAPSHOT'

task createProperties(dependsOn: processResources){
    doLast{
        println("Writing properties file")
        new File("$buildDir/resources/main/version.properties").withWriter {w ->
            Properties p = new Properties()
            p['name'] = name
            p['version'] = project.version.toString()+":"+ LocalDateTime.now().hashCode()
            p.store w, null
        }
    }
}

classes{
    dependsOn createProperties
}

idea{
    module{
        downloadJavadoc = true
    }
}

repositories {
    mavenCentral()
    jcenter()
}

def jme3 = [v:'3.6.0-stable', g:'org.jmonkeyengine']
dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    //jme
    implementation "${jme3.g}:jme3-core:${jme3.v}"
    implementation "${jme3.g}:jme3-plugins:${jme3.v}"
    runtimeOnly "${jme3.g}:jme3-desktop:${jme3.v}"
    runtimeOnly "${jme3.g}:jme3-lwjgl3:${jme3.v}"
    runtimeOnly "${jme3.g}:jme3-networking:${jme3.v}"
    //groovy
    implementation 'org.codehaus.groovy:groovy-all:2.4.21'
    //jme libs
    implementation "com.simsilica:zay-es:1.5.0"
    implementation "com.simsilica:lemur:1.16.0"
    implementation "com.simsilica:lemur-proto:1.13.0"
    implementation 'com.simsilica:sio2:1.8.0'
    implementation 'com.simsilica:sim-math:1.6.0'
    //csv
    implementation("com.github.doyaaaaaken:kotlin-csv-jvm:0.15.2")
    //kotlin serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
}

test {
    useJUnitPlatform()
}